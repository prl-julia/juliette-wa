#lang racket

(require redex)
(require "../core/wa-full.rkt")  ; import language semantics
(provide (all-defined-out))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Optimization Language Extenstions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define-extended-language WA-opt WA-full
  (nv ::= x v)                   ; near value
  
  (Î“ ::= ((x Ï„) ...))            ; environment of types

  (sig-Ïƒ ::= (mdef mname (Ïƒ ...))) ; concrete type method signature
  (sig-Ï„ ::= (mdef mname (Ï„ ...))) ; abstract type method signature
  
  (Î” ::= ((sig-Ï„ real) ...))       ; environment of inlined methods
  (Î¦ ::= ((sig-Ïƒ mname) ...))      ; environment of methods with direct calls
  (opt-err ::= undeclared-var md-err type-err)
  
  (maybe-Ï„ ::= Ï„ opt-err)
  (maybe-e ::= e opt-err)
  (maybe-mname ::= mname nothing)

  (N ::= natural)
  (L ::= N)

  ;; simple optimization context
  [E ::=
      hole    ; â–¡
      (seq E e)  ; E;e
      (seq e E)  ; e;E
      (mcall e ... E e ...)    ;  e(e..., E, e...)
      (pcall op e ... E e ...) ; op(e..., E, e...)
      ]

  ;; optimize state < Î“ Î¦ xe >
  [st-opt ::= (< Î“ Î” Î¦ (evalt MT (in-hole E maybe-e)) >)]
  [st-mtopt ::= (< Î¦ MT L >)])

(define MAX_INLINE_COUNT 3)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Language Extension Helpers
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; ==================================================
;; Typing Environment Helpers
;; ==================================================

;; (lookup Î“ x) retrieves x's type from Î“
(define-metafunction WA-opt
  lookup : Î“ x -> Ï„ or opt-err
  [(lookup ((x_1 Ï„_1) ... (x Ï„) (x_2 Ï„_2) ...) x)
   Ï„
   (side-condition (not (member (term x) (term (x_1 ...)))))]
  [(lookup any_1 any_2) undeclared-var])

(test-equal (term (lookup ((x Int64) (x (mtag "s")) (y Bool)) x)) (term Int64))
(test-equal (term (lookup ((x Int64) (x (mtag "s")) (y Bool)) y)) (term Bool))

;; (extend Î“ (x Ï„) ...) add (x Ï„) to Î“ so that x is found before other x-s
(define-metafunction WA-opt
  extend : Î“ (x Ï„) ... -> Î“
  [(extend ((x_Î“ Ï„_Î“) ...) (x Ï„) ...) ((x Ï„) ...(x_Î“ Ï„_Î“) ...)])

(test-equal (term (extend () (x Int64))) (term ((x Int64))))
(test-equal (term (extend ((x Int64)) (x Bool))) (term ((x Bool) (x Int64))))

;; ==================================================
;; Near-Value Helpers
;; ==================================================

;; Returns type tag of the given near values
(define-metafunction WA-opt
  typeof-nv : Î“ nv -> maybe-Ï„
  [(typeof-nv Î“ v) (typeof v)]
  [(typeof-nv Î“ x) (lookup Î“ x)]
  )

(test-equal (term (typeof-nv () 3)) (term Int64))
(test-equal (term (typeof-nv ((x Bool)) "a")) (term String))
(test-equal (term (typeof-nv ((x Bool)) x)) (term Bool))

;; Returns tuple-type tag of the given tuple of near values
(define-metafunction WA-opt
  typeof-nv-tuple : Î“ (nv ...) -> (maybe-Ï„ ...)
  [(typeof-nv-tuple Î“ (nv ...)) ((typeof-nv Î“ nv) ...)]
  )

(test-equal (term (typeof-nv-tuple () (3 "string")))
            (term (Int64 String)))
(test-equal (term (typeof-nv-tuple ((x Bool) (y Float64) (x Int64)) (x y)))
            (term (Bool Float64)))

;; Determines if the expression is a near value
(define-metafunction WA-opt
  is-nv : e -> boolean
  [(is-nv nv) #t]
  [(is-nv _)  #f])

;; ==================================================
;; Method Table Helpers
;; ==================================================

;; -------------------- Length

;; Determines the length of the method table
(define-metafunction WA-opt
  length : MT -> integer
  [(length âˆ…) 0]
  [(length (_ â€¢ MT_rest)) ,(+ 1 (term (length MT_rest)))]
  )

(define id-fInt (term (mdef "func" ((:: x Int64)) x)))
(define g-return1 (term (mdef "g" () 1)))
(test-equal (term (length âˆ…)) 0)
(test-equal (term (length (,id-fInt â€¢ (,g-return1 â€¢ âˆ…)))) 2)

;; -------------------- Reverse

;; Reverses the given method-table using the given accumulated reverse list
(define-metafunction WA-opt
  reverse-mt-acc : MT MT -> MT
  [(reverse-mt-acc âˆ…         MT_acc) MT_acc]
  [(reverse-mt-acc (md â€¢ MT) MT_acc) (reverse-mt-acc MT (md â€¢ MT_acc))])

;; Reverses the given method-table
(define-metafunction WA-opt
  reverse-mt : MT -> MT
  [(reverse-mt MT) (reverse-mt-acc MT âˆ…)])

(define f-return0 (term (mdef "f" () 0)))
(define g-return0 (term (mdef "g" () 0)))
(define f-g-table (term (,f-return0 â€¢ (,g-return0 â€¢ âˆ…))))
(test-equal (term (reverse-mt âˆ…))
            (term âˆ…))
(test-equal (term (reverse-mt (,g-return0 â€¢ (,f-return0 â€¢ âˆ…))))
            (term ,f-g-table))

;; -------------------- Append

;; Appends the two method tables together
(define-metafunction WA-opt
  append : MT MT -> MT
  [(append âˆ… MT)
   MT]
  [(append (md â€¢ MT_rest) MT_2)
   (md â€¢ (append MT_rest MT_2))])

(test-equal (term (append ,f-g-table âˆ…))
            (term ,f-g-table))
(test-equal (term (append âˆ… ,f-g-table))
            (term ,f-g-table))
(test-equal (term (append âˆ… (,f-return0 â€¢ (,g-return0 â€¢ ,f-g-table))))
            (term (,f-return0 â€¢ (,g-return0 â€¢ ,f-g-table))))

;; -------------------- Cut

;; Takes the first N mdefs from the method table
(define-metafunction WA-opt
  take : natural MT -> MT
  [(take 0 MT) âˆ…]
  [(take N âˆ…)  âˆ…]
  [(take N (md_first â€¢ MT_rest))
   (md_first â€¢ (take ,(- (term N) 1) MT_rest))]
  )

(test-equal (term (take 1 âˆ…)) (term âˆ…))
(test-equal (term (take 1 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term (,id-fInt â€¢ âˆ…)))
(test-equal (term (take 0 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term âˆ…))
(test-equal (term (take 2 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))

;; Removes the first N mdefs from the method table
(define-metafunction WA-opt
  drop : natural MT -> MT
  [(drop 0 MT) MT]
  [(drop N  âˆ…) âˆ…]
  [(drop N (_ â€¢ MT_rest))
   (drop ,(- (term N) 1) MT_rest)]
  )

(test-equal (term (drop 1 âˆ…)) (term âˆ…))
(test-equal (term (drop 1 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term (,g-return1 â€¢ âˆ…)))
(test-equal (term (drop 0 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
(test-equal (term (drop 2 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…)))) (term âˆ…))

;; -------------------- Indexing

;; Gets the i-th method (indexing at 0 where 0 is the outermost element)
(define-metafunction WA-opt
  get-element-wrap : natural MT -> md or nothing
  [(get-element-wrap _ âˆ…)
   nothing]
  [(get-element-wrap 0 (md â€¢ MT_rest))
   md]
  [(get-element-wrap N (_ â€¢ MT_rest))
   (get-element-wrap ,(- (term N) 1) MT_rest)]
  )

;; Gets the ith method (indexing at 0 where 0 is the innermost element)
(define-metafunction WA-opt
  get-element : natural MT -> md or nothing
  [(get-element N MT)
   (get-element-wrap N (reverse-mt MT))]
  )

(test-equal (term (get-element 0 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term ,g-return1))
(test-equal (term (get-element 2 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term nothing))
(test-equal (term (get-element 2 âˆ…)) (term nothing))
(test-equal (term (get-element 1 (,id-fInt â€¢ (,g-return1 â€¢ âˆ…))))
            (term ,id-fInt))

;; ==================================================
;; Syntax and Bindings Helpers
;; ==================================================

;; Determines if the given value is value is a mval
(define-metafunction WA-opt
  is-mval : v -> boolean
  [(is-mval (mval _)) #t]
  [(is-mval _) #f])
(test-equal (term (is-mval 1)) #f)
(test-equal (term (is-mval (mval "func"))) #t)

;; Gets the name of the direct call method if one exists
(define-metafunction WA-opt
  get-opt-method : Î¦ sig-Ïƒ -> mname or nothing
  [(get-opt-method (_ ... (sig-Ïƒ mname_opt) _ ...) sig-Ïƒ)
   mname_opt]
  [(get-opt-method _ _) nothing]
  )

;; Determines if the given local variable has the same name as the given variable
;; The regex match is needed in order to ignore the substitution id of redex vars
(define (same-varname localvar x_str)
  (let* ((localvar_match
          (first (regexp-match* #rx".*Â«" (~a localvar))))
         (localvar_str (substring localvar_match
                                  0 (- (string-length localvar_match) 1))))
    (equal? localvar_str x_str)))

;; Determines whether the given name is used in the given expression
(define-metafunction WA-opt
  contains-name-e : e x -> boolean
  [(contains-name-e (mdef mname ((:: x_local _) ...) e_mbody) x)
   ,(or (equal? (~a (term x)) (term mname))
        (ormap (Î» (localvar) (same-varname localvar (~a (term x))))
               (term (x_local ...)))
        (term (contains-name-e e_mbody x)))]
  [(contains-name-e (evalg e) x)
   (contains-name-e e x)]
  [(contains-name-e (mcall e ...) x)
   ,(ormap (Î» (expr) (term (contains-name-e ,expr x)))
           (term (e ...)))]
  [(contains-name-e (seq e_1 e_2) x)
   ,(or (term (contains-name-e e_1 x))
        (term (contains-name-e e_2 x)))]
  [(contains-name-e (if e_1 e_2 e_3) x)
   ,(or (term (contains-name-e e_1 x))
        (term (contains-name-e e_2 x))
        (term (contains-name-e e_3 x)))]
  [(contains-name-e x x)
   #t]
  [(contains-name-e (mval string_var) x)
   ,(equal? (~a (term x)) (term string_var))]
  [(contains-name-e (pcall op e_1 e_2) x)
   ,(or (term (contains-name-e e_1 x))
        (term (contains-name-e e_2 x)))]
  [(contains-name-e _ _)
   #f])

(test-equal (term (contains-name-e 1 var1)) #f)
(test-equal (term (contains-name-e (mval "func") f)) #f)
(test-equal (term (contains-name-e (mval "f") f)) #t)
(test-equal (term (contains-name-e t t)) #t)
(test-equal (term (contains-name-e test t)) #f)
(test-equal (term (contains-name-e (seq true func) func)) #t)
(test-equal (term (contains-name-e (seq (seq nothing "func") 1.1) func)) #f)
(test-equal (term (contains-name-e (if nothing func 1.1) func)) #t)
(test-equal (term (contains-name-e (evalg (mval "t")) t)) #t)
(test-equal (term (contains-name-e (mcall func test) var)) #f)
(test-equal (term (contains-name-e (mcall add 1 var) var)) #t)
(test-equal (term (contains-name-e (mcall var) var)) #t)
(test-equal (term (contains-name-e (mcall tmp tmp tmp) tmp)) #t)
(test-equal (term (contains-name-e (pcall + 1 (mcall var)) var)) #t)
(test-equal (term (contains-name-e (pcall * 1 1.1) x)) #f)
(test-equal (term (contains-name-e (mdef "test" () 1) x)) #f)
(test-equal (term (contains-name-e (mdef "tst" ((:: y Bool) (:: x String)) 1) x)) #t)
(test-equal (term (contains-name-e (mdef "f" () (pcall + 1 x)) x)) #t)
(test-equal (term (contains-name-e ,g-return1 g)) #t)
(test-equal (term (contains-name-e (mdef "g" () (mcall (mval "g"))) g)) #t)
(test-equal (term (contains-name-e (mdef "g" ((:: f Any) (:: h Any))
                                         (mcall (mval "h") (mcall f))) x)) #f)
(test-equal (term (contains-name-e ,id-fInt x)) #t)

;; Determines whether the given name is used in the given method table
(define-metafunction WA-opt
  contains-name-MT : MT x -> boolean
  [(contains-name-MT âˆ… x)
   #f]
  [(contains-name-MT (md â€¢ MT) x)
   ,(or (term (contains-name-e md x)) (term (contains-name-MT MT x)))])

(test-equal (term (contains-name-MT âˆ… name)) #f)
(test-equal (term (contains-name-MT ((mdef "f" ((:: x Int64)) (evalg var)) â€¢ âˆ…) var)) #t)
(test-equal (term (contains-name-MT ((mdef "f" () (pcall + 1 x))
                                     â€¢ ((mdef "g" () (mcall (mval "g"))) â€¢ âˆ…)) var)) #f)
(test-equal (term (contains-name-MT (,g-return1 â€¢ ((mdef "tst" ((:: y Bool) (:: test String)) 1)
                                           â€¢ ((mdef "test" () 1) â€¢ âˆ…))) test)) #t)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Typing Judgment Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Defines the typing relation on World Age expressions
(define-judgment-form WA-opt
  #:mode (âŠ¢ I I O)
  #:contract (âŠ¢ Î“ e Ïƒ)
  ; Î“ âŠ¢ x :: (mtag "x"), where x âˆ‰ Î“
  [(where undeclared-var (lookup Î“ x))
   ----------------------------------- "T-Method-Var"
   (âŠ¢ Î“ x (mtag ,(~a (term x))))]
  ; Î“ âŠ¢ x :: Ïƒ, where (x :: Ï„) âˆˆ Î“
  [(where Ïƒ (lookup Î“ x))
   ---------------------- "T-Var"
   (âŠ¢ Î“ x Ïƒ)]
  ; Î“ âŠ¢ v :: (typeof v)
  [------------------- "T-Val"
   (âŠ¢ Î“ v (typeof v))]
  ; Î“ âŠ¢ e1;e2 :: Ïƒ, where e2 :: Ïƒ
  [(âŠ¢ Î“ e_2 Ïƒ_2)
   ------------------------ "T-Seq"
   (âŠ¢ Î“ (seq e_1 e_2) Ïƒ_2)]
  ; Î“ âŠ¢ ð›¿(e...) :: Ï„
  [(âŠ¢ Î“ e Ïƒ) ...
   (where Ïƒ_res (res-type-primop op Ïƒ ...))
   ---------------------------------------- "T-Primop"
   (âŠ¢ Î“ (pcall op e ...) Ïƒ_res)]
  ; Î“ âŠ¢ m(...) = e :: (mtag "m") 
  [----------------------------------------------------- "T-MD"
   (âŠ¢ Î“ (mdef mname ((:: x Ï„_arg) ...) e) (mtag mname))]
  ; Î“ âŠ¢ (|e|) :: Ïƒ, where e :: Ïƒ
  [(âŠ¢ Î“ e Ïƒ)
   ------------------ "T-EvalGlobal"
   (âŠ¢ Î“ (evalg e) Ïƒ)]
  )

(test-equal (judgment-holds (âŠ¢ () 1 Int64)) #true)
(test-equal (judgment-holds (âŠ¢ () (evalg m) (mtag "m"))) #true)
(test-equal (judgment-holds (âŠ¢ ((x String) (y Bool) (y Float64)) (seq 4 y) Bool)) #true)
(test-equal (judgment-holds (âŠ¢ ((b Bool)) (pcall && b true) Bool)) #true)
(test-equal (judgment-holds (âŠ¢ ((x String) (y Float64)) (pcall + x 1) Float64)) #false)
(test-equal (judgment-holds (âŠ¢ () (mdef "test" () 1) (mtag "test"))) #true)

;; ==================================================
;; Optimization Judgment for Expression
;; ==================================================

;; Determines if the optimized expression is a valid optimization
(define-judgment-form WA-opt
  #:mode (~~> I I I I)
  #:contract (~~> Î“ Î¦ (evalt MT e) (evalt MT e))
  ; Î“ âŠ¢ Î¦ (|v|)_MT ~~> (|v|)_MT' where v != m
  [(where #f (is-mval v))
   -------------------------------------- "OE-Val"
   (~~> Î“ Î¦ (evalt MT v) (evalt MT_P v))]
  ; Î“ âŠ¢ Î¦ (|m|)_MT ~~> (|m|)_MT'
  [(where #t (inMTdomWrap MT mname))
   ------------------------------------------------------------ "OE-ValFun"
   (~~> Î“ Î¦ (evalt MT (mval mname)) (evalt MT_P (mval mname)))]
  ; Î“ âŠ¢ Î¦ (|x|)_MT ~~> (|x|)_MT'
  [-------------------------------------- "OE-Var"
   (~~> Î“ Î¦ (evalt MT x) (evalt MT_P x))]
  ; Î“ âŠ¢ Î¦ (|(|x|)|)_MT ~~> (|(|x|)|)_MT'
  [------------------------------------------------------ "OE-Global"
   (~~> Î“ Î¦ (evalt MT (evalg e)) (evalt MT_P (evalg e)))]
  ; Î“ âŠ¢ Î¦ (|(|x|)_MT_L|)_MT ~~> (|(|x|)_MT_L|)_MT'
  [---------------------------------------------------------------- "OE-Local"
   (~~> Î“ Î¦ (evalt MT (evalt MT_L e)) (evalt MT_P (evalt MT_L e)))]
  ; Î“ âŠ¢ Î¦ (|md|)_MT ~~> (|md|)_MT'
  [---------------------------------------- "OE-MD"
   (~~> Î“ Î¦ (evalt MT md) (evalt MT_P md))]
  ; Î“ âŠ¢ Î¦ (|e1;e2|)_MT ~~> (|e1';e2'|)_MT
  [(~~> Î“ Î¦ (evalt MT e_1) (evalt MT_P e_1P))
   (~~> Î“ Î¦ (evalt MT e_2) (evalt MT_P e_2P))
   ------------------------------------------ "OE-Seq"
   (~~> Î“ Î¦ (evalt MT (seq e_1 e_2))
          (evalt MT_P (seq e_1P e_2P)))]
  ; Î“ âŠ¢ Î¦ (|ð›¿(e...)|)_MT ~~> (|ð›¿(e'...)|)_MT
  [(~~> Î“ Î¦ (evalt MT e) (evalt MT_P e_P)) ...
   ------------------------------------------- "OE-Primop"
   (~~> Î“ Î¦ (evalt MT (pcall op e ...))
          (evalt MT_P (pcall op e_P ...)))]
  ; Î“ âŠ¢ Î¦ (|m(e...)|)_MT -> (|m(e'...)|)_MT
  [(~~> Î“ Î¦ (evalt MT e_arg) (evalt MT_P e_argP))
   (~~> Î“ Î¦ (evalt MT e) (evalt MT_P e_P)) ...
   ---------------------------------------------- "OE-Call"
   (~~> Î“ Î¦ (evalt MT (mcall e_arg e ...))
          (evalt MT_P (mcall e_argP e_P ...)))]
  ; Î“ âŠ¢ Î¦ (|m(nv...)|)_MT ~~> (|nothing; e_body|)_MT' where is is mval
  [(where (Ïƒ ...) (typeof-nv-tuple Î“ (nv ...)))
   (where (mdef mname ((:: x _) ...) e_mbody)
          (getmd MT mname (Ïƒ ...)))
   (where e_b (subst-n e_mbody (x nv) ...))
   (~~> Î“ Î¦ (evalt MT e_b) (evalt MT_P e_P))
   ----------------------------------------------- "OE-Inline"
   (~~> Î“ Î¦ (evalt MT (mcall (mval mname) nv ...))
          (evalt MT_P (seq nothing e_P)))]
  ; Î“ âŠ¢ Î¦ (|m(e...)|)_MT ~~> (|m_direct(e'...)|)_MT' where m_direct is a singleton method
  [(~~> Î“ Î¦ (evalt MT e) (evalt MT_P e_P)) ...
   (âŠ¢ Î“ e_P Ïƒ) ...
   (where mname_opt (get-opt-method Î¦ (mdef mname (Ïƒ ...))))
   (where #t ,(equal? (term mname_P) (term mname_opt)))
   --------------------------------------------------------- "OE-Specialize"
   (~~> Î“ Î¦ (evalt MT (mcall (mval mname) e ...))
          (evalt MT_P (mcall (mval mname_P) e_P ...)))]
  ; Convert variable to mval
  [(where mname ,(~a (term x_mname)))
   (where undeclared-var (lookup Î“ x_mname))
   (~~> Î“ Î¦ (evalt MT (mcall (mval mname) e ...)) (evalt MT_P e_p))
   ---------------------------------------------------------------- "OE-MName"
    (~~> Î“ Î¦ (evalt MT (mcall x_mname e ...)) (evalt MT_P e_p))]
  )

(define addxy-intNum (term (mdef "add" ((:: x Int64) (:: y Number)) (pcall + x y))))
(define MT-addintNum (term (,addxy-intNum â€¢ âˆ…)))
(define MT-addintint-addintNum (term ((mdef "add_P" ((:: x Int64) (:: y Int64)) (pcall + x y))
                                          â€¢ ,MT-addintNum)))
(test-equal (judgment-holds (~~> ((x Int64)) () (evalt âˆ… x) (evalt âˆ… x))) #t)
(test-equal (judgment-holds (~~> () () (evalt âˆ… (evalg 1.1)) (evalt âˆ… (evalg 1.1)))) #t)
(test-equal (judgment-holds (~~> () () (evalt âˆ… (evalt âˆ… true))
                                 (evalt (,addxy-intNum â€¢ âˆ…) (evalt âˆ… true)))) #t)
(test-equal (judgment-holds (~~> () () (evalt âˆ… (evalt âˆ… true))
                                 (evalt (,addxy-intNum â€¢ âˆ…) (evalt (,addxy-intNum â€¢ âˆ…) true)))) #f)
(test-equal (judgment-holds (~~> () () (evalt âˆ… (evalg 1.1)) (evalt âˆ… (evalg 1.1)))) #t)
(test-equal (judgment-holds (~~> ((x Int64)) () (evalt âˆ… x) (evalt âˆ… x))) #t)
(test-equal (judgment-holds (~~> () () (evalt (,id-fInt â€¢ âˆ…) (mval "func"))
                                 (evalt (,id-fInt â€¢ (,addxy-intNum â€¢ âˆ…)) (mval "func")))) #t)
(test-equal (judgment-holds (~~> () () (evalt âˆ… (mval "func")) (evalt âˆ… (mval "func")))) #f)
(test-equal (judgment-holds (~~> () () (evalt âˆ… ,addxy-intNum) (evalt âˆ… ,addxy-intNum))) #t)
(test-equal (judgment-holds (~~> () () (evalt (,id-fInt â€¢ âˆ…) y)
                                    (evalt (,id-fInt â€¢ âˆ…) y))) #true)
(test-equal (judgment-holds (~~> () () (evalt ((mdef "func" () 3) â€¢ âˆ…) (mcall (mval "func")))
                                    (evalt ((mdef "func" () 3) â€¢ âˆ…) (seq nothing 3)))) #true)
(test-equal (judgment-holds (~~> () () (evalt ((mdef "func" () 3) â€¢ âˆ…) (seq 1 (mcall (mval "func"))))
                                 (evalt ((mdef "func" () 3) â€¢ âˆ…) (seq 1 (seq nothing 3))))) #true)
(test-equal (judgment-holds (~~> ((y Int64)) (((mdef "add" (Int64 Int64)) "add_P"))
                                 (evalt ,MT-addintNum (mcall (mval "add") 1 (pcall + y y)))
                                 (evalt ,MT-addintint-addintNum
                                        (mcall (mval "add_P") 1 (pcall + y y))))) #true)
(test-equal (judgment-holds (~~> ((y Int64)) (((mdef "add" (Int64 Bool)) "add_P"))
                                 (evalt ,MT-addintNum (mcall (mval "add") 1 (pcall + y y)))
                                 (evalt ,MT-addintint-addintNum
                                        (mcall (mval "add_P") 1 (pcall + y y))))) #false)

;; ==================================================
;; Optimization Judgment for Method Definition
;; ==================================================

;; Determines if the optimized method definition is a valid optimization
(define-judgment-form WA-opt
  #:mode (md~~> I I I I I)
  #:contract (md~~> Î¦ MT MT e e)
  [(where e_P (subst-n e_Pbody (x_P x) ...))
   (~~> ((x Ï„) ...) Î¦ (evalt MT e) (evalt MT_P e_P))
   ----------------------------------------------------- "OD-MD"
   (md~~> Î¦ MT MT_P (mdef mname ((:: x Ï„) ...) e)
          (mdef mname ((:: x_P Ï„) ...) e_Pbody))]
  )
(define func-return1 (term (mdef "func" () 1)))
(define new-call-func-withy (term (mdef "new" ((:: y Int64)) (mcall func y))))
(test-equal (judgment-holds (md~~> () (,id-fInt â€¢ (,func-return1 â€¢ âˆ…))
                                   (,id-fInt â€¢ (,func-return1 â€¢ âˆ…))
                                   ,new-call-func-withy
                                   (mdef "new" ((:: x Int64)) (seq nothing x)))) #true)
(test-equal (judgment-holds (md~~> () ((mdef "func" ((:: x Int64)) 1) â€¢ (,id-fInt â€¢ âˆ…))
                                   (,id-fInt â€¢ âˆ…) ,new-call-func-withy
                                   (mdef "new" ((:: x Int64))(seq nothing 1)))) #true)


;; ==================================================
;; Optimization Judgment for Method Table
;; ==================================================

;; -------------------- Helpers

;; Determines if the fourth method table is a valid optimization of the third.
;; This determination is made by assuming the methods of the third and fourth
;; tables are evaluated in the context of the first and second tables respectively
(define-metafunction WA-opt
  related-mt-acc : Î¦ MT MT MT MT -> boolean
  [(related-mt-acc Î¦ MT_orig1 MT_orig2 âˆ… âˆ…) #t]
  [(related-mt-acc Î¦ MT_orig1 MT_orig2 (md â€¢ MT) âˆ…) #f]
  [(related-mt-acc Î¦ MT_orig1 MT_orig2 âˆ… (md â€¢ MT)) #t]
  [(related-mt-acc Î¦ MT_orig1 MT_orig2 (md_1 â€¢ MT_1) (md_2 â€¢ MT_2))
   (related-mt-acc Î¦ MT_orig1 MT_orig2 MT_1 MT_2)
   (side-condition (judgment-holds (md~~> Î¦ MT_orig1 MT_orig2 md_1 md_2)))]
  [(related-mt-acc _ _ _ _ _) #f]
  )

;; Determines if the given name does not exist in the given table
(define-metafunction WA-opt
  not-contain-name : MT mname -> boolean
  [(not-contain-name (md â€¢ MT_rest) mname)
   ,(and (not (term (contains-name-e md ,(string->symbol (term mname)))))
         (term (not-contain-name MT_rest mname)))]
  [(not-contain-name âˆ… mname) #t]
  )

;; Determines if there are no names in the second table that are in the first
(define-metafunction WA-opt
  no-repeat-names : e MT MT -> boolean
  [(no-repeat-names e MT_orig ((mdef mname _ _) â€¢ MT_rest))
   ,(and (not (term (contains-name-e e ,(string->symbol (term mname)))))
         (term (not-contain-name MT_orig mname))
         (term (no-repeat-names e MT_orig MT_rest)))]
  [(no-repeat-names e MT_orig âˆ…) #t]
  )

;; -------------------- Main Rule

;; Determines if the optimized method table is a valid optimization
(define-judgment-form WA-opt
  #:mode (mt~~> I I I I)
  #:contract (mt~~> Î¦ e MT MT)
  [(where N_Len (length MT))
   (where N_PLen (length MT_P))
   (where #t ,(<= (term N_Len) (term N_PLen)))
   (where N_lenDiff ,(- (term N_PLen) (term N_Len)))
   (where #t (related-mt-acc Î¦ MT MT_P MT (drop N_lenDiff MT_P)))
   (where #t (no-repeat-names e MT (take N_lenDiff MT_P)))
   (where #t ,(andmap
               (Î» (sig-mname-pair) (judgment-holds (wd~~> Î¦ MT MT_P ,sig-mname-pair)))
               (term Î¦)))
   -------------------------------------------------------------- "OT-MethodTable"
   (mt~~> Î¦ e MT MT_P)]
  )

(test-equal (judgment-holds (mt~~> () 1 âˆ… âˆ…)) #t)
(test-equal (judgment-holds (mt~~> () true (,id-fInt â€¢ âˆ…) âˆ…)) #f)
(test-equal (judgment-holds (mt~~> () 1 âˆ… (,new-call-func-withy â€¢ âˆ…))) #t)
(test-equal (judgment-holds (mt~~> () false (,id-fInt
                               â€¢(,func-return1
                                 â€¢ (,new-call-func-withy â€¢ âˆ…)))
                              (,id-fInt
                               â€¢(,func-return1
                                 â€¢ ((mdef "new" ((:: x Int64)) (seq nothing x)) â€¢ âˆ…))))) #t)

;; Determines if the optimized method table is a valid optimization
(define-judgment-form WA-opt
  #:mode (wd~~> I I I I)
  #:contract (wd~~> Î¦ MT MT (sig-Ïƒ mname))
  [(where (mdef mname ((:: x Ï„) ...) e_body)
          (getmd MT mname (Ïƒ ...)))
   (where (mdef mname_P ((:: x_P Ï„_P) ...) e_Pbody)
          (getmd MT_P mname_P (Ïƒ ...)))
   (~~> ((x Ïƒ) ...) Î¦ (evalt MT e_body)
        (evalt MT_P (subst-n e_Pbody (x_P x) ...)))
   ------------------------------------------------- "OT-MethodTable"
   (wd~~> Î¦ MT MT_P ((mdef mname (Ïƒ ...)) mname_P))]
  )

;; ==================================================
;; Optimization Reduction Helpers
;; ==================================================

;; -------------------- Typing

;; Gets the type of the given expression
(define-metafunction WA-opt
  get-type :  Î“ e -> maybe-Ï„
  [(get-type Î“ e) Ï„
   (where (âŠ¢ _ _ Ï„)
          ,(let ((type-derivation (build-derivations (âŠ¢ Î“ e Ï„))))
             (if (cons? type-derivation)
                 (derivation-term (first type-derivation)) (term type-err))))]
  [(get-type _ _) type-err]
  )

;; Gets the types of the given expressions
(define-metafunction WA-opt
  get-types :  Î“ e ... -> (maybe-Ï„ ...)
  [(get-types Î“ e ...) ((get-type Î“ e) ...)]
  )

;; -------------------- Direct Call

;; Determines if the direct call env contains the given name
(define-metafunction WA-opt
  contains-name-Î¦ : Î¦ string -> boolean
  [(contains-name-Î¦ (_ ... (sig-Ïƒ string) _ ...) string)
   #t]
  [(contains-name-Î¦ ((sig-Ïƒ string_mname) ...) string_arg)
   #f]
  )

;; Generates a name that is not in the method table or direct call env
(define-metafunction WA-opt
  gen-name : MT Î¦ -> string
  [(gen-name MT Î¦)
   ,(~a (term x_gen))
   (where x_gen ,(gensym))
   (where #f (contains-name-MT MT x_gen))
   (where #f (contains-name-Î¦ Î¦ ,(~a (term x_gen))))])

;; -------------------- Inlining

;; Gets the inline count valued paired to the given signature in the inline env
(define-metafunction WA-opt
  get-inline-count : Î” sig-Ï„ -> natural
  [(get-inline-count (_ ... (sig-Ï„ N_count) _ ...) sig-Ï„)
   N_count]
  [(get-inline-count _ _)
   0]
  )

;; Updates the given signature with the given value in the inline env
(define-metafunction WA-opt
  update-inline-count : Î” sig-Ï„ natural -> Î”
  [(update-inline-count (any_begin ... (sig-Ï„ _) any_end ...) sig-Ï„ N)
   (any_begin ... (sig-Ï„ N) any_end ...)]
  [(update-inline-count (any_list ...) sig-Ï„ N)
   ((sig-Ï„ N) any_list ...)]
  )

;; Updates the given signature with a value of 1 greater than then current in the inline env
(define-metafunction WA-opt
  increment-inline-count : Î” sig-Ï„ -> Î”
  [(increment-inline-count Î” sig-Ï„)
  (update-inline-count Î” sig-Ï„ ,(+ (term (get-inline-count Î” sig-Ï„)) 1))])

;; Gets the signature and optimized method name of the callee of the given method call
(define-metafunction WA-opt
  get-opt-name-and-sig : Î“ Î¦ MT mc -> (< maybe-mname md >) or nothing
  [(get-opt-name-and-sig Î“ Î¦ MT (mcall (mval mname) e ...))
   (< maybe-mname (mdef mname ((:: x Ïƒ) ...) e_body) >)
   (where #f ,(andmap (Î» (expr) (term (is-nv ,expr))) (term (e ...))))
   (where #f (contains-name-Î¦ Î¦ ,(~a (term mname))))
   (where (Ïƒ ...) (get-types Î“ e ...))
   (where (mdef mname ((:: x _) ...) e_body) (getmd MT mname (Ïƒ ...)))
   (where maybe-mname (get-opt-method Î¦ (mdef mname (Ïƒ ...))))]
  [(get-opt-name-and-sig _ _ _ _) nothing])


;; ==================================================
;; Expression Optimization
;; ==================================================

;; < Î“ Î” Î¦ (|X[e]|)_MT > --> < Î“ Î”' Î¦' (|X[e']|)_MT' >
(define ->optimize
  (reduction-relation 
   WA-opt
   #:domain st-opt
   ; < Î“ Î” Î¦ (|X[m(nv...)]|)_MT > --> < Î“ Î”' Î¦ (|X[nothing;e]|)_MT >
   ; where e is is m body
   [--> (< Î“ Î” Î¦ (evalt MT (in-hole E (mcall (mval mname) nv ...))) >)
        (< Î“ Î”_P Î¦ (evalt MT (in-hole E (seq nothing e))) >)
        (where (Ïƒ ...) (typeof-nv-tuple Î“ (nv ...)))
        (where (mdef mname ((:: x Ï„) ...) e_mbody) (getmd MT mname (Ïƒ ...)))
        (where sig-Ï„ (mdef mname (Ï„ ...)))
        (where N_count (get-inline-count Î” sig-Ï„))
        (side-condition (< (term N_count) MAX_INLINE_COUNT))
        (where Î”_P (increment-inline-count Î” sig-Ï„))
        (where e (subst-n e_mbody (x nv) ...))
        OE-Inline]
   ; Convert variable to mval
   [--> (< Î“ Î” Î¦ (evalt MT (in-hole E (mcall x_mname e ...))) >)
        (< Î“ Î” Î¦ (evalt MT (in-hole E (mcall (mval mname) e ...))) >)
        (where mname ,(~a (term x_mname)))
        (where undeclared-var (lookup Î“ x_mname))
        OE-MName]
   ; < Î“ Î” Î¦ (|X[m(e...)]|)_MT > --> < Î“ Î” Î¦ (|X[m_direct(e...)]|)_MT >
   ; where (m(Ï„...) m_direct) âˆˆ Î¦
   [--> (< Î“ Î” Î¦ (evalt MT (in-hole E (mcall (mval mname) e ...))) >)
        (< Î“ Î” Î¦ (evalt MT (in-hole E (mcall (mval mname_opt) e ...))) >)
        (where mc (mcall (mval mname) e ...))
        (where (< mname_opt _ >) (get-opt-name-and-sig Î“ Î¦ MT mc))
        OE-Direct-Existing]
   ; < Î“ Î” Î¦ (|X[m(e...)]|)_MT > --> < Î“ Î” Î¦' (|X[m_direct(e...)]|)_MT >
   ; where (m(Ï„...) m_direct) âˆ‰ Î¦
   [--> (< Î“ Î” Î¦ (evalt MT (in-hole E (mcall (mval mname) e ...))) >)
        (< Î“ Î” Î¦_P (evalt MT_P (in-hole E (mcall (mval mname_opt) e ...))) >)
        (where mc (mcall (mval mname) e ...))
        (where (< nothing (mdef mname ((:: x Ïƒ) ...) e_body) >)
               (get-opt-name-and-sig Î“ Î¦ MT mc))
        (where mname_opt (gen-name MT Î¦))
        (where md_opt (mdef mname_opt ((:: x Ïƒ) ...) e_body))
        (where MT_P (md_opt â€¢ MT))
        (where (any_optpair ...) Î¦)
        (where Î¦_P (((mdef mname (Ïƒ ...)) mname_opt) any_optpair ...))
        OE-Direct-New]
))

;; Generates the optimized method table
(define-metafunction WA-opt
  generate-mtopt : natural L MT md MT -> MT
  [(generate-mtopt N_MTlen L MT md MT_P) MT_PP
   (where MT_0toL-1 (drop ,(- (term N_MTlen) (term L)) MT))
   (where MT_0toL (md â€¢ MT_0toL-1))
   (where MT_L+1toN (take ,(- (term N_MTlen) (term L) 1) MT))
   (where N_MTPlen (length MT_P))
   (where MT_N+1toK (take ,(- (term N_MTPlen) (term N_MTlen)) MT_P))
   (where MT_PP (append MT_N+1toK (append MT_L+1toN MT_0toL)))])


;; ==================================================
;; Method Table Optimization
;; ==================================================

;; (< Î¦ MT L >) (< Î¦' MT' L' >)
(define ->optimize-mt
  (reduction-relation 
   WA-opt
   #:domain st-mtopt
   [--> (< Î¦ MT L >) (< Î¦_P MT_PP L_P >)
        (where N_MTlen (length MT))
        (side-condition (< (term L) (term N_MTlen)))
        (where (mdef mname ((:: x Ï„) ...) e_body) (get-element L MT))
        (where ((< _ _ Î¦_P (evalt MT_P e_bodyP) >) _ ...)
               ,(apply-reduction-relation*
                 ->optimize
                 (term (< ((x Ï„) ...) () Î¦ (evalt MT e_body) >))))
        (where md_opt (mdef mname ((:: x Ï„) ...) e_bodyP))
        (where MT_PP (generate-mtopt N_MTlen L MT md_opt MT_P))
        (where L_P ,(+ 1 (term L)))
        OE-Mt]
   ))

;; Optimizes the given method table
(define-metafunction WA-opt
  opt-mt : MT -> ((< Î¦ MT_opt L >) ...)
  [(opt-mt MT)
   ,(apply-reduction-relation*
            ->optimize-mt
            (term (< () MT 0 >)))])

;; Optimizes the given expression
(define-metafunction WA-opt
  opt-e : Î“ MT e -> (< Î¦ MT e >)
  [(opt-e Î“ MT_in e_in)
   (< Î¦_out MT_out e_out >)
   (where ((< Î¦ MT_opt _ >) _ ...) (opt-mt MT_in))
   (where ((< Î“_out Î”_out Î¦_out (evalt MT_out e_out) >) _ ...)
          ,(apply-reduction-relation*
            ->optimize
            (term (< Î“ () Î¦ (evalt MT_opt e_in) >))))]
  )

;; ==================================================
;; Optimization Reduction/Judgment Correspondence
;; ==================================================

; Determines if the optimzaton of the given expression a
; valid optimizaton given the optimization judgments
(define-metafunction WA-opt
  valid-optimization : Î“ MT e -> boolean
  [(valid-optimization Î“ MT_in e_in)
   ,(and (judgment-holds (mt~~> Î¦_out e_in MT_in MT_out))
         (judgment-holds (~~> Î“ Î¦_out (evalt MT_in e_in) (evalt MT_out e_out))))
   (where (< Î¦_out MT_out e_out >) (opt-e Î“ MT_in e_in))]
  [(valid-optimization _ _ _) #f])

;;;;;;;;;;;;;;;
;; Optimizer
;;;;;;;;;;;;;;;

(define ->step-opt
  (reduction-relation 
   WA-opt
   #:domain st
   ; <MTg, C[x]> where x \in dom(MTg) --> <MTg, (mval "x")>
   [--> (< MT_g (in-hole C x) >)
        (< MT_g (in-hole C (mval mname)) >)
        (where #t (inMTdom MT_g x))
        ; transform variable name to a string
        (where mname ,(~a (term x)))
        E-VarMethod]
   ; <MTg, C[v;e]> --> <MTg, C[e]>
   [--> (< MT_g (in-hole C (seq v e)) >)
        (< MT_g (in-hole C e) >)
        E-Seq]
   ; <MTg, C[(if true e_1 e_2)]> --> <MTg, C[e_1]>
   [--> (< MT_g (in-hole C (if true e_1 e_2)) >)
        (< MT_g (in-hole C e_1) >)
        E-IfTrue]
   ; <MTg, C[(if false e_1 e_2)]> --> <MTg, C[e_2]>
   [--> (< MT_g (in-hole C (if false e_1 e_2)) >)
        (< MT_g (in-hole C e_2) >)
        E-IfFalse]
   ; <MTg, C[op(v...)]> --> <MTg, C[v']>
   [--> (< MT_g (in-hole C (pcall op v ...)) >)
        (< MT_g (in-hole C v_r) >)
        (where v_r (run-primop op v ...))
        E-Primop]
   ; <MTg, C[(|v|)]> --> <MTg, C[v]>
   [--> (< MT_g (in-hole C (evalg v)) >)
        (< MT_g (in-hole C v) >)
        E-ValGlobal]
   ; <MTg, C[(|v|)_MT]> --> <MTg, C[v]>
   [--> (< MT_g (in-hole C (evalt MT v)) >)
        (< MT_g (in-hole C v) >)
        E-ValLocal]
   ; <MTg, C[md]> --> <MTg, C[nothing]>
   [--> (< MT_g        (in-hole C md) >)
        (< (md â€¢ MT_g) (in-hole C (mval mname)) >)
        (where (mdef mname _ _) md)
        E-MD]
   ; <MTg, C[(| X[m(v...)] |)]> --> <MTg, C[(|X[ (|m(v...)|)_MTg ]|)]>
   [--> (< MT_g (in-hole C (evalg (in-hole X (mcall (mval mname) v ...)))) >)
        (< MT_g (in-hole C (evalg (in-hole X (evalt MT_gP (mcall (mval mname) v ...))))) >)
        (where ((< _ MT_gP _ >) _ ...) (opt-mt MT_g))
        E-CallGlobal]
   ; <MTg, C[(| X[m(v...)] |)_MT]> --> <MTg, C[(| X[e[x...:=v...]] |)_MT]>
   [--> (< MT_g (in-hole C (evalt MT (in-hole X (mcall (mval mname) v ...)))) >)
        (< MT_g (in-hole C (evalt MT (in-hole X (subst-n e (x v) ...)))) >)
        (where (Ïƒ ...) (typeof-tuple (v ...)))
        (where (mdef mname ((:: x _) ...) e) (getmd MT mname (Ïƒ ...)))
        E-CallLocal]
))

;; Runs program normally while possible but optimized
(define-metafunction WA-full
  run-normal-opt : p -> (st ...)
  [(run-normal-opt p) 
   ,(apply-reduction-relation* ->step-opt (term (< âˆ… p >)))]
)

;; Runs program to normally then runs program in error reduction
(define-metafunction WA-full
  run-opt : p -> (stf ...)
  [(run-opt p)
   ; for every end state of normal execution, try step to an error
   ; (if the state st is good, (run-error st) will simply return the same (st) back))
   (flatten ((run-error st) ...))
   ; run program normally
   (where (st ...) (run-normal-opt p))]
)

;; Evaluates a program in the empty table
;; and returns only the result
;; (throwing away the resulting global table)
(define-metafunction WA-full
  run-to-r-opt : p -> r
  [(run-to-r-opt p)
   r
   (where (( < MT r >)) (run-opt p))]
)
